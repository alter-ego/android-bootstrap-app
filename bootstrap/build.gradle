buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_tools_version"
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath "me.tatarka:gradle-retrolambda:$retrolambda_gradle_version"
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../jacoco.gradle'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://github.com/alter-ego/advanced-android-logger/raw/develop/releases/" }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion compile_sdk
    buildToolsVersion "$build_tools_version"

    defaultConfig {
        applicationId "solutions.alterego.bootstrap"
        minSdkVersion min_sdk
        targetSdkVersion target_sdk
        versionCode app_version_code
        versionName "$app_version_name"
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true

            // Extra fields used to configure Robolectric test suites.
            buildConfigField "int", "TEST_TARGET_SDK_VERSION", "25"
            buildConfigField "String", "TEST_APPLICATION_ID", "\"solutions.alterego.bootstrap.debug\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Extra fields used to configure Robolectric test suites.
            buildConfigField "int", "TEST_TARGET_SDK_VERSION", "25"
            buildConfigField "String", "TEST_APPLICATION_ID", "\"solutions.alterego.bootstrap\""
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true

        unitTests.all {
            jvmArgs '-Xmx512m'
            systemProperty 'robolectric.logging.enable', 'true'
        }
    }

    tasks.withType(Test) {
        testLogging {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$support_libraries_version"
    compile "com.android.support:design:$support_libraries_version"
    compile 'com.alterego:advancedandroidlogger:1.0.8@aar'
    compile 'com.android.support:multidex:1.0.1'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.hannesdorfmann.fragmentargs:annotation:2.0.1'
    apt 'com.hannesdorfmann.fragmentargs:processor:2.0.1'
    apt "org.projectlombok:lombok:$lombok_version"
    provided "org.projectlombok:lombok:$lombok_version"
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    compile "org.parceler:parceler-api:0.2.16"
    provided "org.parceler:parceler:0.2.16"
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.1.0'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'

    compile 'com.google.code.gson:gson:2.6.1'
    compile 'net.danlew:android.joda:2.9.0'

    compile 'com.facebook.fresco:fresco:0.9.0'

    compile "com.facebook.stetho:stetho:$stetho_version"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    androidTestCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    compile 'com.squareup.haha:haha:2.0.3' //leakcanary uses this, prints lots of warnings otherwise

    compile('com.mikepenz.materialdrawer:library:2.9.2@aar') {
        transitive = true
    }

    //unit testing
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.github.fabioCollini:DaggerMock:0.5'

    testCompile('junit:junit:4.12') {
        exclude module: 'hamcrest'
        exclude module: 'hamcrest-core'
    }
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile('org.mockito:mockito-core:1.10.19') {
        exclude module: 'hamcrest'
        exclude module: 'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.github.fabioCollini:DaggerMock:0.5'
    testCompile "org.robolectric:robolectric:3.0"
    testCompile "org.robolectric:shadows-multidex:3.0"
}
